# BaRS response validator ruleset used by tkw autotest
# NHS Interoperability Toolkit
#
#
#
# *****************************************************************************************

VALIDATION-RULESET-NAME FHIR BaRS autotest response validations
VALIDATION-RULESET-VERSION 0.9
VALIDATION-RULESET-TIMESTAMP 20230524
VALIDATION-RULESET-AUTHOR Messaging and Integration Team

#==========================================================================================
# get booking
VALIDATE urn:nhs:names:services:bars:fhir:rest:read:appointment

IF xpathexists /fhir:OperationOutcome
THEN
	CHECK hapifhirvalidator
	ANNOTATION "Operation Outcome hapifhir profile validation"
ELSE
	CHECK hapifhirvalidator
	ANNOTATION "Appointment hapifhir profile validation"
	CHECK sub request_contains_required_headers
ENDIF

#==========================================================================================
# new booking request
VALIDATE urn:nhs:names:services:bars:fhir:messaging:new:booking-request

IF xpathexists /fhir:OperationOutcome
THEN
	CHECK hapifhirvalidator
	ANNOTATION "Operation Outcome hapifhir profile validation"
ELSE
	CHECK hapifhirvalidator
	ANNOTATION "new booking-request hapifhir profile validation"
	CHECK sub request_contains_required_headers
	CHECK sub dos_service_id_system_urls_correct
	CHECK sub patient_contact
	CHECK sub patient_birthSex
	CHECK sub booking_request_payload
	CHECK sub last_updated_instant
ENDIF

#==========================================================================================
# update booking request
VALIDATE urn:nhs:names:services:bars:fhir:messaging:update:booking-request

IF xpathexists /fhir:OperationOutcome
THEN
	CHECK hapifhirvalidator
	ANNOTATION "Operation Outcome hapifhir profile validation"
ELSE
	CHECK hapifhirvalidator
	ANNOTATION "update booking-request hapifhir profile validation"
	CHECK sub request_contains_required_headers
	CHECK sub dos_service_id_system_urls_correct
	CHECK sub patient_contact
	CHECK sub patient_birthSex
	CHECK sub booking_request_payload
	CHECK sub last_updated_instant
ENDIF

#==========================================================================================
# get capability statement
VALIDATE urn:nhs:names:services:bars:fhir:rest:read:metadata

IF xpathexists /fhir:OperationOutcome
THEN
	CHECK hapifhirvalidator
	ANNOTATION "Operation Outcome hapifhir profile validation"
ELSE
	CHECK hapifhirvalidator
	ANNOTATION "CapabilityStatement hapifhir profile validation"
	CHECK sub request_contains_required_headers
ENDIF

#==========================================================================================
# get slots
VALIDATE urn:nhs:names:services:bars:fhir:rest:search:slot

IF xpathexists /fhir:OperationOutcome
THEN
	CHECK hapifhirvalidator
	ANNOTATION "Operation Outcome hapifhir profile validation"
ELSE
	CHECK hapifhirvalidator
	ANNOTATION "Slots hapifhir profile validation"
	CHECK sub request_contains_required_headers
ENDIF

#==========================================================================================
# get validation/referral

VALIDATE urn:nhs:names:services:bars:fhir:rest:read:servicerequest

IF xpathexists /fhir:OperationOutcome
THEN
	CHECK hapifhirvalidator
	ANNOTATION "Operation Outcome hapifhir profile validation"
ELSE
	CHECK hapifhirvalidator
	ANNOTATION ServiceRequest hapifhir profile validation"
	CHECK sub request_contains_required_headers
ENDIF

#==========================================================================================
# new validation/referral request

VALIDATE urn:nhs:names:services:bars:fhir:messaging:new:servicerequest-request

IF xpathexists /fhir:OperationOutcome
THEN
	CHECK hapifhirvalidator
	ANNOTATION "Operation Outcome hapifhir profile validation"
ELSE
	CHECK hapifhirvalidator
	ANNOTATION "new servicerequest-request hapifhir profile validation"
	CHECK sub request_contains_required_headers
	CHECK sub dos_service_id_system_urls_correct
	CHECK sub servicerequest_all_payload
	CHECK sub servicerequest_request_payload
	CHECK sub patient_contact
	CHECK sub patient_birthSex
	CHECK sub servicerequest_new_status
	CHECK sub last_updated_instant
ENDIF

#==========================================================================================
# update/cancel validation/referral request

VALIDATE urn:nhs:names:services:bars:fhir:messaging:update:servicerequest-request

IF xpathexists /fhir:OperationOutcome
THEN
	CHECK hapifhirvalidator
	ANNOTATION "Operation Outcome hapifhir profile validation"
ELSE
	CHECK hapifhirvalidator
	ANNOTATION "update servicerequest-request hapifhir profile validation"
	CHECK sub request_contains_required_headers
	CHECK sub dos_service_id_system_urls_correct
	CHECK sub servicerequest_all_payload
	CHECK sub servicerequest_request_payload
	CHECK sub patient_contact
	CHECK sub patient_birthSex
	CHECK sub last_updated_instant
ENDIF

#==========================================================================================
# new validation/referral response

VALIDATE urn:nhs:names:services:bars:fhir:messaging:new:servicerequest-response

IF xpathexists /fhir:OperationOutcome
THEN
	CHECK hapifhirvalidator
	ANNOTATION "Operation Outcome hapifhir profile validation"
ELSE
	CHECK hapifhirvalidator
	ANNOTATION "new servicerequest-response hapifhir profile validation"
	CHECK sub request_contains_required_headers
	CHECK sub dos_service_id_system_urls_correct
	CHECK sub servicerequest_all_payload
	CHECK sub servicerequest_response_payload
	CHECK sub patient_contact
	CHECK sub patient_birthSex
	CHECK sub servicerequest_response_status
	CHECK sub last_updated_instant
ENDIF

#==========================================================================================
# update validation/referral response

VALIDATE urn:nhs:names:services:bars:fhir:messaging:update:servicerequest-response

IF xpathexists /fhir:OperationOutcome
THEN
	CHECK hapifhirvalidator
	ANNOTATION "Operation Outcome hapifhir profile validation"
ELSE
	CHECK hapifhirvalidator
	ANNOTATION "update servicerequest-response hapifhir profile validation"
	CHECK sub request_contains_required_headers
	CHECK sub dos_service_id_system_urls_correct
	CHECK sub servicerequest_all_payload
	CHECK sub servicerequest_response_payload
	CHECK sub patient_contact
	CHECK sub patient_birthSex
	CHECK sub servicerequest_response_status
	CHECK sub last_updated_instant
ENDIF

#==========================================================================================
# get message definition

VALIDATE urn:nhs:names:services:bars:fhir:rest:read:messagedefinition

IF xpathexists /fhir:OperationOutcome
THEN
	CHECK hapifhirvalidator
	ANNOTATION "Operation Outcome hapifhir profile validation"
ELSE
	CHECK hapifhirvalidator
	ANNOTATION "MessageDefinition hapifhir profile validation"
	CHECK sub request_contains_required_headers
ENDIF

#==========================================================================================
# check that the request contains the required headers

SUBSET request_contains_required_headers

CHECK http_header X-Request-Id matches ^.+$
ANNOTATION "Required http header X-Request-Id - RR-HTTP-HEAD or BR-HTTP-HEAD"
CHECK http_header X-Correlation-Id matches ^.+$
ANNOTATION "Required http header X-Correlation-Id - RR-HTTP-HEAD or BR-HTTP-HEAD"
CHECK http_header Content-type matches ^application/fhir\+(xml|json)(;charset=(?i)utf-8)?$
ANNOTATION "Content-type http header is a valid MIME-type - RR-HTTP-HEAD or BR-HTTP-HEAD"

#==========================================================================================
# Validation/Referral request/response payload must include the required resources

SUBSET servicerequest_all_payload

IF xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding/fhir:code/@value validation
THEN
	CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Flag[fhir:category/fhir:coding/fhir:code/@value="SS"]) 1
	ANNOTATION "A scene safety flag must be included in validation requests and responses - RR-REC-SCENE-INFO"
	CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Location
	ANNOTATION "Validation request/response should send location details - RR-REC-INCID-INFO"
	IF xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Location/fhir:extension[@url="https://fhir.nhs.uk/StructureDefinition/LocationExtension"]/fhir:extension[@url="UPRN"]
	THEN
		CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Location/fhir:extension[@url="https://fhir.nhs.uk/StructureDefinition/LocationExtension"]/fhir:extension[@url="UPRN"]/fhir:valueString/@value ^[0-9]{1,12}$
		ANNOTATION "UPRN must be valid - RR-INFO-LOC-UPRN"
	ENDIF
	IF xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Location/fhir:extension[@url="https://fhir.nhs.uk/StructureDefinition/LocationExtension"]/fhir:extension[@url="eastings"]
	THEN
		CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Location/fhir:extension[@url="https://fhir.nhs.uk/StructureDefinition/LocationExtension"]/fhir:extension[@url="eastings"]/fhir:valueString/@value ^-?[0-9]*\.?[0-9]+$
		ANNOTATION "Eastings must be valid - RR-INFO-LOC-EN"
	ENDIF
	IF xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Location/fhir:extension[@url="https://fhir.nhs.uk/StructureDefinition/LocationExtension"]/fhir:extension[@url="northings"]
	THEN
		CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Location/fhir:extension[@url="https://fhir.nhs.uk/StructureDefinition/LocationExtension"]/fhir:extension[@url="northings"]/fhir:valueString/@value ^-?[0-9]*\.?[0-9]+$
		ANNOTATION "Northings must be valid - RR-INFO-LOC-EN"
	ENDIF
	IF xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Location/fhir:position
	THEN
		CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Location/fhir:position/fhir:longitude/@value ^-?[0-9]*\.?[0-9]+$
		ANNOTATION "Longitude must be valid"
		CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Location/fhir:position/fhir:latitude/@value ^-?[0-9]*\.?[0-9]+$
		ANNOTATION "Latitude must be valid"
	ENDIF
ENDIF

#==========================================================================================
# Validation/Referral request payload must include the required resources

SUBSET servicerequest_request_payload

CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Consent
ANNOTATION "Validation/Referral request payload must include a Consent resource"
CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:Consent/fhir:category/fhir:coding/fhir:code/@value DRC
ANNOTATION "The minimum level of consent should be Direct Care - derived from RS-CONSENT"

CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:name[fhir:use/@value="official"]) 1
ANNOTATION "Patient resource must have a name with use set to official - RR-PATIENT-RESO-NAME"
CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:name[fhir:use/@value="official"]/fhir:family/@value .+
ANNOTATION "Patient resource official name must have family name present - RR-PATIENT-RESO-NAME"
CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:name[fhir:use/@value="official"]/fhir:given/@value .+
ANNOTATION "Patient resource official name must have at least one given name - RR-PATIENT-RESO-NAME"

#==========================================================================================
# Validation/Referral response payload must include the required resources

SUBSET servicerequest_response_payload


#==========================================================================================
# contact information for patient must be stored under patient.contact rather than patient.telecom

SUBSET patient_contact

CHECK xpathnotexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:telecom
ANNOTATION "Contact details for the patient must not be stored under patient.telecom - RR-PAT-CON or BR-PAT-CON"
CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:contact/fhir:extension[@url="https://fhir.hl7.org.uk/StructureDefinition/Extension-UKCore-ContactRank"]/fhir:valuePositiveInt/@value=1) 1
ANNOTATION "Patient resource should have exactly one contact with a UKCore-ContactRank of 1 - RR-PAT-CON or BR-PAT-CON"
CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:contact)=count(fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:contact/fhir:extension[@url="https://fhir.hl7.org.uk/StructureDefinition/Extension-UKCore-ContactRank"]) true
ANNOTATION "Each contact of the Patient resource must include a Extension-UKCore-ContactRank - RR-PAT-CON or BR-PAT-CON"

#==========================================================================================
# patient birthSex should not be stored on the Patient resource

SUBSET patient_birthSex

CHECK xpathnotexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:extension[@url="https://fhir.hl7.org.uk/StructureDefinition/Extension-UKCore-BirthSex"]
ANNOTATION "The birthSex must be transferred via an Observation Resource and should not be present on the Patient Resource - derived from RS-PATIENT-SEX or BS-PATIENT-SEX"

#==========================================================================================
# key statuses must be correct when sending a new validation/referral request

SUBSET servicerequest_new_status

IF xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding/fhir:code/@value referral
THEN
	CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:status/@value active
	ANNOTATION "The ServiceRequest status must be active when sending a new referral request - derived from RS-REF-STAT"
	CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Encounter/fhir:status/@value ^(triaged|finished)$
	ANNOTATION "The Encounter status must be triaged or finished when sending a new referral request - derived from RS-REF-STAT"
	CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:CarePlan/fhir:status/@value completed
	ANNOTATION "The CarePlan status must be completed when sending a new referral request - derived from RS-REF-STAT"
ENDIF

IF xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding/fhir:code/@value validation
THEN
	CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:status/@value active
	ANNOTATION "The ServiceRequest status must be active when sending a new validation request - derived from RS-REF-VALID-REQ"
	CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:Encounter/fhir:status/@value triaged
	ANNOTATION "The Encounter status must be triaged when sending a new validation request - derived from RS-REF-VALID-REQ"
	CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:CarePlan/fhir:status/@value active
	ANNOTATION "The CarePlan status must be active when sending a new validation request - derived from RS-REF-VALID-REQ"
ENDIF

#==========================================================================================
# key statuses must be correct when sending a validation/referral response

SUBSET servicerequest_response_status

IF xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding/fhir:code/@value validation
THEN
	IF xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Encounter[fhir:status/@value="finished"]) 1
	THEN
		CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:status/@value completed
		ANNOTATION "The ServiceRequest status must be completed when sending a full validation response - derived from RS-REF-FULL-VALID-RES"
		CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:CarePlan/fhir:status/@value ^(completed|active)$
		ANNOTATION "The CarePlan status must be completed or active when sending a full validation response - derived from RS-REF-FULL-VALID-RES"
	ENDIF
	IF xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Encounter[fhir:status/@value="in-progress"]) 1
	THEN
		CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:status/@value active
		ANNOTATION "The ServiceRequest status must be active when sending an interim validation response - derived from RR-REF-INT-VALID-RES"
	ENDIF
ENDIF

IF xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding/fhir:code/@value referral
THEN
	IF xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:status/@value revoked
	THEN
		IF xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Encounter
		THEN
			CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:Encounter/fhir:status/@value cancelled
			ANNOTATION "If a new Encounter is created during a Safeguarding DNA response, then its status must be cancelled"
		ENDIF
		IF xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Appointment
		THEN
			CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:Appointment/fhir:status/@value noshow
			ANNOTATION "If a new Appointment is created during a Safeguarding DNA response, then its status must be noshow"
		ENDIF
	ENDIF
ENDIF

#==========================================================================================
# the booking request payload must be valid

SUBSET booking_request_payload

CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Appointment
ANNOTATION "Booking request payload must include the Appointment resource - derived from BS-PATIENT-RESO-REF"
CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient
ANNOTATION "Booking request payload must include the Patient resource - derived from BS-PATIENT-RESO-REF"
CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Slot
ANNOTATION "Booking request payload must include the Slot resource - derived from BS-PATIENT-RESO-REF"
CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Schedule
ANNOTATION "Booking request payload must include the Schedule resource - derived from BS-PATIENT-RESO-REF"
CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:HealthcareService
ANNOTATION "Booking request payload must include the HealthcareService resource - derived from BS-PATIENT-RESO-REF"

CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:name[fhir:use/@value="official"]) 1
ANNOTATION "Patient resource must have a name with use set to official - derived from BS-PATIENT-RESO-NAME-OFFI"
CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:name[fhir:use/@value="official"]/fhir:family/@value .+
ANNOTATION "Patient resource official name must have family name present - derived from BS-PATIENT-RESO-NAME-OFFI"
CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:name[fhir:use/@value="official"]/fhir:given/@value .+
ANNOTATION "Patient resource official name must have at least one given name - derived from BS-PATIENT-RESO-NAME-OFFI"

CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:contact/fhir:telecom/fhir:system/@value="phone")>=1 true
ANNOTATION "Patient resource must have a contact set with a system of phone - derived from BS-PATIENT-RESO-PHONE"
CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:contact/fhir:telecom[fhir:system/@value="phone"]/fhir:value/@value ^[ 0-9]+$
ANNOTATION "Patient resource contact value must be a valid UK phone number - derived from BS-PATIENT-RESO-PHONE"

CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:address
ANNOTATION "Patient resource must have at least one address - derived from BS-PATIENT-RESO-ADDRESS"

CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:address[fhir:use/@value="home"]) 1
ANNOTATION "Patient resource must have at exactly one address with use set to home - derived from BS-PATIENT-RESO-HOME"

CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:address[fhir:use/@value="home"]/fhir:postalCode/@value ^[ 0-9a-zA-Z]+$
ANNOTATION "The home address of the Patient resource must have a valid postcode - derived from BS-PATIENT-RESO-POSTCODE"

CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:birthDate
ANNOTATION "Patient resource must have a date of birth value - derived from BS-PATIENT-RESO-DOB"

#==========================================================================================
# Common validations

INCLUDE TKW_ROOT/config/FHIR_BaRS/validator_config/bars_common.conf
