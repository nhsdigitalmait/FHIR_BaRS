# BaRS validator ruleset used by tkw simulator
# NHS Interoperability Toolkit
# 
# 
# 
# *****************************************************************************************

VALIDATION-RULESET-NAME FHIR BaRS Configuration Ruleset 
VALIDATION-RULESET-VERSION 0.1
VALIDATION-RULESET-TIMESTAMP April 2022
VALIDATION-RULESET-AUTHOR NHS Digital

#==========================================================================================

# get booking
VALIDATE urn:nhs:names:services:bars:fhir:rest:read:appointment
CHECK sub request_contains_required_headers
CHECK sub request_contains_optional_headers

# new referral request
VALIDATE urn:nhs:names:services:bars:fhir:messaging:new:servicerequest-request
CHECK hapifhirvalidator
ANNOTATION "HAPI FHIR Validation"
CHECK sub request_contains_required_headers
CHECK sub request_contains_optional_headers
CHECK sub request_with_body_contains_headers
CHECK sub servicerequest_request_payload
CHECK sub code_display
CHECK sub appointment_supportinginfo
CHECK sub patient_contact
CHECK sub servicerequest_new_status

# update/cancel referral request
VALIDATE urn:nhs:names:services:bars:fhir:messaging:update:servicerequest-request
CHECK hapifhirvalidator
ANNOTATION "HAPI FHIR Validation"
CHECK sub request_contains_required_headers
CHECK sub request_contains_optional_headers
CHECK sub request_with_body_contains_headers
CHECK sub servicerequest_request_payload
CHECK sub code_display
CHECK sub patient_contact

# new referral response
VALIDATE urn:nhs:names:services:bars:fhir:messaging:new:servicerequest-response
CHECK hapifhirvalidator
ANNOTATION "HAPI FHIR Validation"
CHECK sub request_contains_required_headers
CHECK sub request_contains_optional_headers
CHECK sub request_with_body_contains_headers
CHECK sub servicerequest_response_payload
CHECK sub code_display
CHECK sub patient_contact
CHECK sub new_validation_response_status

# update referral response
VALIDATE urn:nhs:names:services:bars:fhir:messaging:update:servicerequest-response
CHECK hapifhirvalidator
ANNOTATION "HAPI FHIR Validation"
CHECK sub request_contains_required_headers
CHECK sub request_contains_optional_headers
CHECK sub request_with_body_contains_headers
CHECK sub servicerequest_response_payload
CHECK sub code_display
CHECK sub patient_contact
CHECK sub update_validation_response_status

# new booking request
VALIDATE urn:nhs:names:services:bars:fhir:messaging:new:booking-request
CHECK hapifhirvalidator
ANNOTATION "HAPI FHIR Validation"
CHECK sub request_contains_required_headers
CHECK sub request_contains_optional_headers
CHECK sub request_with_body_contains_headers
CHECK sub patient_contact
CHECK sub booking_request_payload

# update booking request
VALIDATE urn:nhs:names:services:bars:fhir:messaging:update:booking-request
CHECK hapifhirvalidator
ANNOTATION "HAPI FHIR Validation"
CHECK sub request_contains_required_headers
CHECK sub request_contains_optional_headers
CHECK sub request_with_body_contains_headers
CHECK sub patient_contact
CHECK sub booking_request_payload

# get message definition
VALIDATE urn:nhs:names:services:bars:fhir:rest:read:messagedefinition
CHECK sub request_contains_required_headers
# optional headers not part of the api specification but required for tkw
CHECK sub request_contains_optional_headers

# get capability statement
VALIDATE urn:nhs:names:services:bars:fhir:rest:read:metadata
CHECK sub request_contains_required_headers
# optional headers not part of the api specification but required for tkw
CHECK sub request_contains_optional_headers

# get referral
VALIDATE urn:nhs:names:services:bars:fhir:rest:read:servicerequest
CHECK sub request_contains_required_headers
CHECK sub request_contains_optional_headers

# get slots
VALIDATE urn:nhs:names:services:bars:fhir:rest:search:slot
CHECK sub request_contains_required_headers
CHECK sub request_contains_optional_headers
CHECK sub search_slots_url

#==========================================================================================
# check that the request contains the required headers

SUBSET request_contains_required_headers

CHECK http_header NHSD-Target-Identifier matches ^.+$
ANNOTATION "Required http header NHSD-Target-Identifier - RS-HTTP-HEAD or BS-HTTP-HEAD"
CHECK http_header X-Request-Id matches ^.+$
ANNOTATION "Required http header X-Request-Id - RS-HTTP-HEAD or BS-HTTP-HEAD"
CHECK http_header X-Correlation-Id matches ^.+$
ANNOTATION "Required http header X-Correlation-Id - RS-HTTP-HEAD or BS-HTTP-HEAD"

IF context_path notmatches ^.*_format.*$
THEN
	CHECK http_header Accept matches ^application/fhir\+(xml|json)(;charset=utf-8)?$
	ANNOTATION "Accept http header should be set to application/fhir+json or application/fhir+xml"
ENDIF

#==========================================================================================
# check that the request contains the optional headers

SUBSET request_contains_optional_headers

CHECK http_header NHSD-End-User-Organisation matches ^.+$
ANNOTATION "WARNING Optional http header NHSD-End-User-Organisation - RS-HTTP-HEAD or BS-HTTP-HEAD"
CHECK http_header NHSD-Requesting-Practitioner matches ^.+$
ANNOTATION "WARNING Optional http header NHSD-Requesting-Practitioner - RS-HTTP-HEAD or BS-HTTP-HEAD"
CHECK http_header NHSD-Requesting-Person matches ^.+$
ANNOTATION "WARNING Optional http header NHSD-Requesting-Person - RS-HTTP-HEAD or BS-HTTP-HEAD"
CHECK http_header NHSD-Requesting-Software matches ^.+$
ANNOTATION "WARNING Optional http header NHSD-Requesting-Software - RS-HTTP-HEAD or BS-HTTP-HEAD"

#==========================================================================================
# check that the request with a body contains the required headers

SUBSET request_with_body_contains_headers

CHECK http_header Content-type matches ^application/fhir\+(xml|json)(;charset=utf-8)?$
ANNOTATION "Content-type http header is a valid MIME-type - RS-HTTP-HEAD or BS-HTTP-HEAD"

#==========================================================================================
# service request payload (request) must be a bundle and include at least the required resources

SUBSET servicerequest_request_payload

CHECK xpathexists /fhir:Bundle
ANNOTATION "Service request (request) payload must be a Bundle - RS-PAYLOAD-SERV-REQ"
CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:MessageHeader
ANNOTATION "Service request (request) payload must include the MessageHeader resource - RS-PAYLOAD-SERV-REQ"
CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient
ANNOTATION "Service request (request) payload must include the Patient resource - RS-PAYLOAD-SERV-REQ"
CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest
ANNOTATION "Service request (request) payload must include the ServiceRequest resource - RS-PAYLOAD-SERV-REQ"

CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Organization
ANNOTATION "Service request (request) payload must include the Organization resource - RS-PAYLOAD-SERV-REQ"
CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:PractitonerRole
ANNOTATION "Service request (request) payload must include the PractitonerRole resource - RS-PAYLOAD-SERV-REQ"
CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Practitioner
ANNOTATION "Service request (request) payload must include the Practitioner resource - RS-PAYLOAD-SERV-REQ"

#==========================================================================================
# service request payload (response) must be a bundle and include at least the required resources

SUBSET servicerequest_response_payload

CHECK xpathexists /fhir:Bundle
ANNOTATION "Service request (response) payload must be a Bundle - RS-PAYLOAD-SERV-REQ"
CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:MessageHeader
ANNOTATION "Service request (response) payload must include the MessageHeader resource - RS-PAYLOAD-SERV-REQ"
CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient
ANNOTATION "Service request (response) payload must include the Patient resource - RS-PAYLOAD-SERV-REQ"
CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest
ANNOTATION "Service request (response) payload must include the ServiceRequest resource - RS-PAYLOAD-SERV-REQ"

CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:HealthcareService
ANNOTATION "Service request (response) payload must include the HealthcareService resource - RS-PAYLOAD-SERV-REQ"
CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Organization
ANNOTATION "Service request (response) payload must include the Organization resource - RS-PAYLOAD-SERV-REQ"

#==========================================================================================
# when coded data is transmitted always include display text

SUBSET code_display

# TODO check whether this test is still needed
CHECK xpathequals boolean(every $x in //fhir:coding satisfies $x/display) true
ANNOTATION "Include display text when sending coded data - RS-CODE-DISPLAY"

#==========================================================================================
# if there is both a booking and referral then supporting info reference must be recorded

SUBSET appointment_supportinginfo

# we are using a specific nhs number for this scenario
IF xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:identifier/fhir:value/@value 9054613556
THEN
	CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:supportingInfo
	ANNOTATION "If there is both a booking and a referral then the supporting info reference must be recorded - RS-INC-BOOK"
ENDIF

#==========================================================================================
# contact information for patient must be stored under patient.contact rather than patient.telecom

SUBSET patient_contact

CHECK xpathnotexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:telecom
ANNOTATION "Contact details for the patient must not be stored under patient.telecom - RS-PAT-CON or BS-PAT-CON"
CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:contact/fhir:extension[@url="https://fhir.hl7.org.uk/StructureDefinition/Extension-UKCore-ContactRank"]/fhir:valuePositiveInt/@value=1) 1
ANNOTATION "Patient resource should have exactly one contact with a UKCore-ContactRank of 1 - RS-PAT-CON or BS-PAT-CON"
CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:contact)=count(fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:contact/fhir:extension[@url="https://fhir.hl7.org.uk/StructureDefinition/Extension-UKCore-ContactRank"]) true
ANNOTATION "Each contact of the Patient resource must include a Extension-UKCore-ContactRank - RS-PAT-CON or BS-PAT-CON"

#==========================================================================================
# service request status must be correct when sending a new validation/referral request

SUBSET servicerequest_new_status

IF xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding/fhir:code/@value referral
THEN
	CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:status/@value active
	ANNOTATION "The status must be active when sending a new referral request - RS-REF-STAT"
ENDIF

IF xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding/fhir:code/@value validation
THEN
	CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:status/@value active
	ANNOTATION "The status must be active when sending a new validation request - RS-REF-VALID-REQ"
ENDIF

#==========================================================================================
# service request status must be correct when sending a new validation response

SUBSET new_validation_response_status

CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:status/@value active
ANNOTATION "The service request status must be active when sending an interim validation response - RR-REF-INT-VALID-RES"
CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:Encounter/fhir:status/@value in-progress
ANNOTATION "The encounter status must be in-progress when sending an interim validation response - RR-REF-INT-VALID-RES"

#==========================================================================================
# service request status must be correct when sending an update validation response

SUBSET update_validation_response_status

CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:status/@value completed
ANNOTATION "The service request status must be completed when sending an interim validation response - RS-REF-FULL-VALID-RES"
CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:Encounter/fhir:status/@value triaged
ANNOTATION "The encounter status must be triaged when sending an interim validation response - RS-REF-FULL-VALID-RES"

#==========================================================================================
# url for search slots must be formatted correctly and contain the required parameters

SUBSET search_slots_url

CHECK context_path matches ^.*/Slot\?.*$
ANNOTATION "The URL must start with /Slot? - BS-URL-SLOT"

CHECK context_path matches ^.*status=(free|busy|free%2Cbusy|busy%2Cfree)(&.*$|$)
ANNOTATION "The URL must contain a search parameter of status which is either free, busy, or both - BS-STAT-CRITE-FREE-BUSY"

CHECK context_path matches ^.*start=l[te]([0-9]([0-9]([0-9][1-9]|[1-9]0)|[1-9]00)|[1-9]000)-(0[1-9]|1[0-2])-(0[1-9]|[1-2][0-9]|3[0-1])T([01][0-9]|2[0-3])%3A[0-5][0-9]%3A([0-5][0-9]|60)(\.[0-9]+)?(Z|(%2B|-)((0[0-9]|1[0-3])%3A[0-5][0-9]|14%3A00))(&.*$|$)
ANNOTATION "The URL must contain a search parameter of start with a correctly formed lt or le timestamp - BS-SEARCH-CRITE-END"

CHECK context_path matches ^.*start=g[te]([0-9]([0-9]([0-9][1-9]|[1-9]0)|[1-9]00)|[1-9]000)-(0[1-9]|1[0-2])-(0[1-9]|[1-2][0-9]|3[0-1])T([01][0-9]|2[0-3])%3A[0-5][0-9]%3A([0-5][0-9]|60)(\.[0-9]+)?(Z|(%2B|-)((0[0-9]|1[0-3])%3A[0-5][0-9]|14%3A00))(&.*$|$)
ANNOTATION "The URL must include a search parameter of start with a correctly formed gt or ge timestamp - BS-SEARCH-CRITE-BEGIN"

CHECK context_path matches ^.*Schedule.actor%3AHealthcareService=[0-9]+(&.*$|$)
ANNOTATION "The URL must contain a search parameter of Schedule.actor:HealthcareService - BS-SEARCH-CRITE-SERV-ID"

#==========================================================================================
# the booking request payload must be valid

SUBSET booking_request_payload

CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Appointment
ANNOTATION "Booking request payload must include the Appointment resource - BS-PATIENT-RESO-REF"
CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient
ANNOTATION "Booking request payload must include the Patient resource - BS-PATIENT-RESO-REF"
CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Slot
ANNOTATION "Booking request payload must include the Slot resource - BS-PATIENT-RESO-REF"
CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Schedule
ANNOTATION "Booking request payload must include the Schedule resource - BS-PATIENT-RESO-REF"
CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:HealthcareService
ANNOTATION "Booking request payload must include the HealthcareService resource - BS-PATIENT-RESO-REF"

CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:name/fhir:use/@value official
ANNOTATION "Patient resource must have name with use set to official - BS-PATIENT-RESO-NAME-OFFI"
CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:name/fhir:family/@value .+
ANNOTATION "Patient resource must have name with family value not set to null - BS-PATIENT-RESO-NAME-OFFI"
CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:name/fhir:given/@value .+
ANNOTATION "Patient resource must have name with given value not set to null - BS-PATIENT-RESO-NAME-OFFI"

CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:contact[fhir:relationship/fhir:coding/fhir:code/@value="ONESELF"]/fhir:telecom/fhir:system/@value phone
ANNOTATION "Patient resource must have a contact set with a system of phone - BS-PATIENT-RESO-PHONE"
CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:contact[fhir:relationship/fhir:coding/fhir:code/@value="ONESELF"]/fhir:telecom/fhir:value/@value ^[ 0-9]+$
ANNOTATION "Patient resource contact value must be a valid UK phone number - BS-PATIENT-RESO-PHONE"

CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:address
ANNOTATION "Patient resource must have at least one address - BS-PATIENT-RESO-ADDRESS"

CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:address/fhir:use/@value="home") 1
ANNOTATION "Patient resource must have at exactly one address with use set to home - BS-PATIENT-RESO-HOME"

CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:address[fhir:use/@value="home"]/fhir:postalCode/@value ^[ 0-9a-zA-Z]+$
ANNOTATION "The home address of the Patient resource must have a valid postcode - BS-PATIENT-RESO-POSTCODE"

CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:birthDate
ANNOTATION "Patient resource must have a date of birth value - BS-PATIENT-RESO-DOB"










