# BaRS validator ruleset used by tkw simulator
# NHS Interoperability Toolkit
# 
# 
# 
# *****************************************************************************************

VALIDATION-RULESET-NAME FHIR BaRS Simulator Configuration Ruleset 
VALIDATION-RULESET-VERSION 1.0.16
VALIDATION-RULESET-TIMESTAMP 20250305
VALIDATION-RULESET-AUTHOR Messaging and Integration Team

#==========================================================================================

# get booking
VALIDATE urn:nhs:names:services:bars:fhir:rest:read:appointment
CHECK sub request_contains_required_headers

# new validation/referral request
VALIDATE urn:nhs:names:services:bars:fhir:messaging:new:servicerequest-request
CHECK hapifhirvalidator
ANNOTATION "new servicerequest-request hapifhir profile validation"
CHECK sub request_contains_required_headers
CHECK sub request_with_body_contains_headers
CHECK sub dos_service_id_system_urls_correct
CHECK sub servicerequest_all_payload
CHECK sub servicerequest_request_payload
CHECK sub servicerequest_new_request_payload
CHECK sub servicerequest_category_referraltopharmacy
CHECK sub servicerequest_category_CADtoCAD
CHECK sub code_display
CHECK sub appointment_supportinginfo
CHECK sub patient_contact
CHECK sub patient_birthSex
CHECK sub servicerequest_new_status
CHECK sub last_updated_instant
CHECK sub messdefcheck

# update validation request / cancel referral request
VALIDATE urn:nhs:names:services:bars:fhir:messaging:update:servicerequest-request
CHECK hapifhirvalidator
ANNOTATION "update servicerequest-request hapifhir profile validation"
CHECK sub request_contains_required_headers
CHECK sub request_with_body_contains_headers
CHECK sub dos_service_id_system_urls_correct
CHECK sub update_code_correct
CHECK sub servicerequest_all_payload
CHECK sub servicerequest_request_payload
CHECK sub servicerequest_cancel_request_payload
CHECK sub servicerequest_category_referraltopharmacy
CHECK sub servicerequest_category_CADtoCAD
CHECK sub code_display
CHECK sub patient_contact
CHECK sub patient_birthSex
CHECK sub last_updated_instant

# new validation/referral response
VALIDATE urn:nhs:names:services:bars:fhir:messaging:new:servicerequest-response
CHECK hapifhirvalidator
ANNOTATION "new servicerequest-response hapifhir profile validation"
CHECK sub request_contains_required_headers
CHECK sub request_with_body_contains_headers
CHECK sub dos_service_id_system_urls_correct
CHECK sub servicerequest_all_payload
CHECK sub servicerequest_response_payload
CHECK sub servicerequest_category_referraltopharmacy
CHECK sub code_display
CHECK sub patient_contact
CHECK sub patient_birthSex
CHECK sub servicerequest_response_status
CHECK sub last_updated_instant

# update validation response
VALIDATE urn:nhs:names:services:bars:fhir:messaging:update:servicerequest-response
CHECK hapifhirvalidator
ANNOTATION "update servicerequest-response hapifhir profile validation"
CHECK sub request_contains_required_headers
CHECK sub request_with_body_contains_headers
CHECK sub dos_service_id_system_urls_correct
CHECK sub servicerequest_all_payload
CHECK sub servicerequest_response_payload
CHECK sub servicerequest_category_referraltopharmacy
CHECK sub code_display
CHECK sub patient_contact
CHECK sub patient_birthSex
CHECK sub servicerequest_response_status
CHECK sub last_updated_instant

# new booking request
VALIDATE urn:nhs:names:services:bars:fhir:messaging:new:booking-request
CHECK hapifhirvalidator
ANNOTATION "new booking-request hapifhir profile validation"
CHECK sub request_contains_required_headers
CHECK sub request_with_body_contains_headers
CHECK sub dos_service_id_system_urls_correct
CHECK sub patient_contact
CHECK sub patient_birthSex
CHECK sub booking_request_payload
CHECK sub booking_request_ids
CHECK sub last_updated_instant

# cancel booking request
VALIDATE urn:nhs:names:services:bars:fhir:messaging:update:booking-request
CHECK hapifhirvalidator
ANNOTATION "update booking-request hapifhir profile validation"
CHECK sub request_contains_required_headers
CHECK sub request_with_body_contains_headers
CHECK sub dos_service_id_system_urls_correct
CHECK sub patient_contact
CHECK sub patient_birthSex
CHECK sub booking_cancel_request_payload
CHECK sub last_updated_instant

# get message definition
VALIDATE urn:nhs:names:services:bars:fhir:rest:read:messagedefinition
CHECK sub request_contains_required_headers
CHECK sub context_format_is_correct

# get capability statement
VALIDATE urn:nhs:names:services:bars:fhir:rest:read:metadata
CHECK sub request_contains_required_headers

# get validation/referral
VALIDATE urn:nhs:names:services:bars:fhir:rest:read:servicerequest
CHECK sub request_contains_required_headers

# get slots
VALIDATE urn:nhs:names:services:bars:fhir:rest:search:slot
CHECK sub request_contains_required_headers
CHECK sub search_slots_url

#==========================================================================================
# check that the request contains the required headers

SUBSET request_contains_required_headers

CHECK http_header NHSD-Target-Identifier matches ^.+$
ANNOTATION "Required http header NHSD-Target-Identifier - RS-HTTP-HEAD or BS-HTTP-HEAD"
CHECK http_header X-Request-Id matches ^.+$
ANNOTATION "Required http header X-Request-Id - RS-HTTP-HEAD or BS-HTTP-HEAD"
CHECK http_header X-Correlation-Id matches ^.+$
ANNOTATION "Required http header X-Correlation-Id - RS-HTTP-HEAD or BS-HTTP-HEAD"
CHECK http_header NHSD-End-User-Organisation matches ^.+$
ANNOTATION "Required http header NHSD-End-User-Organisation - RS-HTTP-HEAD or BS-HTTP-HEAD"
#NBRS-3166
#CHECK http_header NHSD-Requesting-Practitioner matches ^.+$
#ANNOTATION "Required http header NHSD-Requesting-Practitioner - RS-HTTP-HEAD or BS-HTTP-HEAD"
CHECK http_header NHSD-Requesting-Software matches ^.+$
ANNOTATION "Required http header NHSD-Requesting-Software - RS-HTTP-HEAD or BS-HTTP-HEAD"

CHECK http_header Accept matches ^application/fhir\+(xml|json).*$
ANNOTATION "Accept http header should be set to application/fhir+json or application/fhir+xml"
CHECK http_header Accept matches ^.*version=.*$
ANNOTATION "Accept http header should mention the version of the API required"



#==========================================================================================
# check that the request with a body contains the required headers

SUBSET request_with_body_contains_headers

CHECK http_header Content-type matches ^application/fhir\+(xml|json)(;.+)?$
ANNOTATION "Content-type http header is a valid MIME-type - RS-HTTP-HEAD or BS-HTTP-HEAD"

CHECK http_header use-context matches ^a[0-9]t[0-9][|](referral|validation)[|]servicerequest(-request|-response)[|](new|update)$
ANNOTATION "Required use-context http header"

#==========================================================================================
# check that the context parameter is formatted correctly
SUBSET context_format_is_correct
#NBRS-3165
CHECK context_path matches ^.*context=https%3A%2F%2F.+%7C.*$
ANNOTATION "The context parameter should be formatted as system|value"


SUBSET update_code_correct
CHECK xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:MessageHeader/fhir:reason/fhir:coding/fhir:code/@value='update') true
ANNOTATION MessageHeader.reason.coding.code must be 'update'

#==========================================================================================
# Validation/Referral request/response payload must include the required resources

SUBSET servicerequest_all_payload

IF xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding/fhir:code/@value validation
THEN
	CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Flag[fhir:category/fhir:coding/fhir:code/@value="SS"]) 1
	ANNOTATION "A scene safety flag must be included in validation requests and responses - RS-REC-SCENE-INFO"
	CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Location
	ANNOTATION "Validation request/response should send location details - RS-REC-INCID-INFO"
	IF xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Location/fhir:extension[@url="https://fhir.nhs.uk/StructureDefinition/LocationExtension"]/fhir:extension[@url="UPRN"]
	THEN
		CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Location/fhir:extension[@url="https://fhir.nhs.uk/StructureDefinition/LocationExtension"]/fhir:extension[@url="UPRN"]/fhir:valueString/@value ^[0-9]{1,12}$
		ANNOTATION "UPRN must be valid - RS-INFO-LOC-UPRN"
	ENDIF
	IF xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Location/fhir:extension[@url="https://fhir.nhs.uk/StructureDefinition/LocationExtension"]/fhir:extension[@url="eastings"]
	THEN
		CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Location/fhir:extension[@url="https://fhir.nhs.uk/StructureDefinition/LocationExtension"]/fhir:extension[@url="eastings"]/fhir:valueString/@value ^-?[0-9]*\.?[0-9]+$
		ANNOTATION "Eastings must be valid - RS-INFO-LOC-EN"
	ENDIF
	IF xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Location/fhir:extension[@url="https://fhir.nhs.uk/StructureDefinition/LocationExtension"]/fhir:extension[@url="northings"]
	THEN
		CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Location/fhir:extension[@url="https://fhir.nhs.uk/StructureDefinition/LocationExtension"]/fhir:extension[@url="northings"]/fhir:valueString/@value ^-?[0-9]*\.?[0-9]+$
		ANNOTATION "Northings must be valid - RS-INFO-LOC-EN"
	ENDIF
	IF xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Location/fhir:position
	THEN
		CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Location/fhir:position/fhir:longitude/@value ^-?[0-9]*\.?[0-9]+$
		ANNOTATION "Longitude must be valid"
		CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Location/fhir:position/fhir:latitude/@value ^-?[0-9]*\.?[0-9]+$
		ANNOTATION "Latitude must be valid"
	ENDIF
ENDIF

#==========================================================================================
# Validation/Referral request payload must include the required resources

SUBSET servicerequest_request_payload

CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:name[fhir:use/@value="official"]) 1
ANNOTATION "Patient resource must have a name with use set to official - RS-PATIENT-RESO-NAME"
CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:name[fhir:use/@value="official"]/fhir:family/@value ^.+$
ANNOTATION "Patient resource official name must have family name present - RS-PATIENT-RESO-NAME"
CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:name[fhir:use/@value="official"]/fhir:given/@value ^.+$
ANNOTATION "Patient resource official name must have at least one given name - RS-PATIENT-RESO-NAME"

#Use Case Category for ServiceRequest
CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding/fhir:system[@value='https://fhir.nhs.uk/CodeSystem/usecases-categories-bars']
ANNOTATION "Use Case Category must be included. See: https://simplifier.net/guide/nhsbookingandreferralstandard/Home/Core/1.1.5/Standard-Pattern-Composite-Messages/Use-Case-Categories"
CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding[fhir:system/@value='https://fhir.nhs.uk/CodeSystem/usecases-categories-bars']/fhir:code/@value
ANNOTATION "Use Case Category Code must be included. See: https://simplifier.net/nhsbookingandreferrals/usecases-categories-bars"
CHECK xpathmatches //fhir:ServiceRequest/fhir:category/fhir:coding[fhir:system/@value='https://fhir.nhs.uk/CodeSystem/usecases-categories-bars']/fhir:code/@value ^a(1t[1-9]|2t[1-4]|3t1|[4-6]t[1-3])|referraltopharmacy$
ANNOTATION "Use Case Category Code must must include ServiceRequest.category values from 'system' 'https://fhir.nhs.uk/CodeSystem/usecases-categories-bars'"

#==========================================================================================
# Validation/Referral new request payload must include the required resources

SUBSET servicerequest_new_request_payload

CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Consent
ANNOTATION "Validation/Referral request payload must include a Consent resource - RS-CONSENT"
CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:Consent/fhir:category/fhir:coding/fhir:code/@value DRC
ANNOTATION "The minimum level of consent should be Direct Care - RS-CONSENT"

#==========================================================================================
# Validation/Referral cancel request payload must include the required resources

SUBSET servicerequest_cancel_request_payload

IF xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:MessageHeader/fhir:definition/@value https://fhir.nhs.uk/MessageDefinition/bars-message-servicerequest-request-cancelled
THEN

#ServiceRequest.category values from 'system' 'https://fhir.nhs.uk/CodeSystem/message-category-servicerequest' and 'https://fhir.nhs.uk/CodeSystem/usecases-categories-bars' must exist

	CHECK xpathmatches //fhir:ServiceRequest/fhir:category/fhir:coding[fhir:system/@value='https://fhir.nhs.uk/CodeSystem/message-category-servicerequest']/fhir:code/@value ^validation|referral|request$
	ANNOTATION "Cancel request payload must include ServiceRequest.category values from 'system' 'https://fhir.nhs.uk/CodeSystem/message-category-servicerequest'"
#The resources in the bundle request must align with https://simplifier.net/NHSBookingandReferrals/MessageDefinition-BARSMessageDefinitionServiceRequestRequestCancelled/~json
	CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:MessageHeader) 1
	ANNOTATION "Cancel request payload must include a MessageHeader resource"
	CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest) 1
	ANNOTATION "Cancel request payload must include a ServiceRequest resource"
	CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient) 1
	ANNOTATION "Cancel request payload must include a Patient resource"
	CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Organization)>=2 true
	ANNOTATION "Cancel request payload must include at least 2 Organization resources"

#MessageHeader.eventCoding.code must equal 'servicerequest-request'
	CHECK xpathequals //fhir:MessageHeader/fhir:eventCoding/fhir:code/@value servicerequest-request
	ANNOTATION "Cancel request payload MessageHeader.eventCoding.code must equal 'servicerequest-request'"
#MessageHeader.reasonCode.code must equal 'update'
	CHECK xpathequals //fhir:MessageHeader/fhir:reason/fhir:coding/fhir:code/@value update
	ANNOTATION "Cancel request payload MessageHeader.reason.coding.code must equal 'update'"
#MessageHeader.defintion must equal 'https://fhir.nhs.uk/MessageDefinition/bars-message-servicerequest-request-cancelled'
	CHECK xpathequals fhir:Bundle/fhir:entry/fhir:resource/fhir:MessageHeader/fhir:definition/@value https://fhir.nhs.uk/MessageDefinition/bars-message-servicerequest-request-cancelled
	ANNOTATION "Cancel Request payload MessageHeader.definition must equal 'https://fhir.nhs.uk/MessageDefinition/bars-message-servicerequest-request-cancelled'"
#MessageHeader.focus must reference the ServiceRequest resource in the bundle 
	CHECK xpathequals boolean(//fhir:ServiceRequest/../../fhir:fullUrl/@value=//fhir:MessageHeader/fhir:focus/fhir:reference/@value) true
	ANNOTATION "Cancel Request payload MessageHeader.focus must reference the ServiceRequest resource in the bundle"
#Maintain existing HTTP Header checks

ENDIF

#==========================================================================================
# Validation/Referral response payload must include the required resources

SUBSET servicerequest_response_payload


#==========================================================================================
# when coded data is transmitted include display text

SUBSET code_display

CHECK xpathequals number(count(//fhir:coding) - count(//fhir:coding/fhir:display)) 0
ANNOTATION "Include display text when sending coded data, if possible - RS-CODE-DISPLAY"

#==========================================================================================
# if there is both a booking and referral then supporting info reference must be recorded

SUBSET appointment_supportinginfo

# we are using a specific nhs number for this scenario
IF xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:identifier/fhir:value/@value 9658499007
THEN
	CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:supportingInfo
	ANNOTATION "If there is both a booking and a referral then the supporting info reference must be recorded - RS-INC-BOOK"
ENDIF

#==========================================================================================
# contact information for patient must be stored under patient.contact rather than patient.telecom

SUBSET patient_contact

CHECK xpathnotexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:telecom
ANNOTATION "Contact details for the patient must not be stored under patient.telecom - RS-PAT-CON or BS-PAT-CON"
CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:contact/fhir:extension[@url="https://fhir.hl7.org.uk/StructureDefinition/Extension-UKCore-ContactRank"]/fhir:valuePositiveInt/@value=1) 1
ANNOTATION "Patient resource should have exactly one contact with a UKCore-ContactRank of 1 - RS-PAT-CON or BS-PAT-CON"
CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:contact)=count(fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:contact/fhir:extension[@url="https://fhir.hl7.org.uk/StructureDefinition/Extension-UKCore-ContactRank"]) true
ANNOTATION "Each contact of the Patient resource must include a Extension-UKCore-ContactRank - RS-PAT-CON or BS-PAT-CON"


#==========================================================================================
# patient birthSex should not be stored on the Patient resource

SUBSET patient_birthSex

CHECK xpathnotexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:extension[@url="https://fhir.hl7.org.uk/StructureDefinition/Extension-UKCore-BirthSex"]
ANNOTATION "The birthSex must be transferred via an Observation Resource and should not be present on the Patient Resource - RS-PATIENT-SEX or BS-PATIENT-SEX

#==========================================================================================
# key statuses must be correct when sending a new validation/referral request

SUBSET servicerequest_new_status

IF xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding/fhir:code/@value referral
THEN
	CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:status/@value active
	ANNOTATION "The ServiceRequest status must be active when sending a new referral request - RS-REF-STAT"
	CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Encounter/fhir:status/@value ^(triaged|finished)$
	ANNOTATION "The Encounter status must be triaged or finished when sending a new referral request - RS-REF-STAT"
	CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:CarePlan/fhir:status/@value completed
	ANNOTATION "The CarePlan status must be completed when sending a new referral request - RS-REF-STAT"
ENDIF

IF xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding/fhir:code/@value validation
THEN
	CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:status/@value active
	ANNOTATION "The ServiceRequest status must be active when sending a new validation request - RS-REF-VALID-REQ"
	CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:Encounter/fhir:status/@value triaged
	ANNOTATION "The Encounter status must be triaged when sending a new validation request - RS-REF-VALID-REQ"
	CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:CarePlan/fhir:status/@value active
	ANNOTATION "The CarePlan status must be active when sending a new validation request - RS-REF-VALID-REQ"
ENDIF

#==========================================================================================
# key statuses must be correct when sending a validation/referral response

SUBSET servicerequest_response_status

IF xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding/fhir:code/@value validation
THEN
	IF xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Encounter[fhir:status/@value="finished"]) 1
	THEN
		CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:status/@value completed
		ANNOTATION "The ServiceRequest status must be completed when sending a full validation response - RS-REF-FULL-VALID-RES"
		CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:CarePlan/fhir:status/@value ^(completed|active)$
		ANNOTATION "The CarePlan status must be completed or active when sending a full validation response - RS-REF-FULL-VALID-RES"
	ENDIF
	IF xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Encounter[fhir:status/@value="in-progress"]) 1
	THEN
		CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:status/@value active
		ANNOTATION "The ServiceRequest status must be active when sending an interim validation response - RR-REF-INT-VALID-RES"
	ENDIF
ENDIF

IF xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding/fhir:code/@value referral
THEN
	IF xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:status/@value revoked
	THEN
		IF xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Encounter
		THEN
			CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:Encounter/fhir:status/@value cancelled
			ANNOTATION "If a new Encounter is created during a Safeguarding DNA response, then its status must be cancelled"
		ENDIF
		IF xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Appointment
		THEN
			CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:Appointment/fhir:status/@value noshow
			ANNOTATION "If a new Appointment is created during a Safeguarding DNA response, then its status must be noshow"
		ENDIF
	ENDIF
ENDIF

#==========================================================================================
# url for search slots must be formatted correctly and contain the required parameters

SUBSET search_slots_url

CHECK context_path matches ^.*/Slot\?.*$
ANNOTATION "The URL must start with /Slot? - BS-URL-SLOT"

CHECK context_path matches ^.*status=(free|busy|free%2Cbusy|busy%2Cfree)(&.*$|$)
ANNOTATION "The URL must contain a search parameter of status which is either free, busy, or both - BS-STAT-CRITE-FREE-BUSY"

CHECK context_path matches ^.*start=l[te]([0-9]([0-9]([0-9][1-9]|[1-9]0)|[1-9]00)|[1-9]000)-(0[1-9]|1[0-2])-(0[1-9]|[1-2][0-9]|3[0-1])T([01][0-9]|2[0-3])%3A[0-5][0-9]%3A([0-5][0-9]|60)(\.[0-9]+)?(Z|(%2B|-)((0[0-9]|1[0-3])%3A[0-5][0-9]|14%3A00))(&.*$|$)
ANNOTATION "The URL must contain a search parameter of start with a correctly formed lt or le timestamp - BS-SEARCH-CRITE-END"

CHECK context_path matches ^.*start=g[te]([0-9]([0-9]([0-9][1-9]|[1-9]0)|[1-9]00)|[1-9]000)-(0[1-9]|1[0-2])-(0[1-9]|[1-2][0-9]|3[0-1])T([01][0-9]|2[0-3])%3A[0-5][0-9]%3A([0-5][0-9]|60)(\.[0-9]+)?(Z|(%2B|-)((0[0-9]|1[0-3])%3A[0-5][0-9]|14%3A00))(&.*$|$)
ANNOTATION "The URL must include a search parameter of start with a correctly formed gt or ge timestamp - BS-SEARCH-CRITE-BEGIN"

CHECK context_path matches ^.*Schedule.actor%3AHealthcareService=[0-9]+(&.*$|$)
ANNOTATION "The URL must contain a search parameter of Schedule.actor:HealthcareService - BS-SEARCH-CRITE-SERV-ID"

#==========================================================================================
# the booking request payload must be valid

SUBSET booking_request_payload

CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Appointment
ANNOTATION "Booking request payload must include the Appointment resource - BS-PATIENT-RESO-REF"
CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient
ANNOTATION "Booking request payload must include the Patient resource - BS-PATIENT-RESO-REF"
CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Slot
ANNOTATION "Booking request payload must include the Slot resource - BS-PATIENT-RESO-REF"
CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Schedule
ANNOTATION "Booking request payload must include the Schedule resource - BS-PATIENT-RESO-REF"
CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:HealthcareService
ANNOTATION "Booking request payload must include the HealthcareService resource - BS-PATIENT-RESO-REF"

CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:name[fhir:use/@value="official"]) 1
ANNOTATION "Patient resource must have a name with use set to official - BS-PATIENT-RESO-NAME-OFFI"
CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:name[fhir:use/@value="official"]/fhir:family/@value ^.+$
ANNOTATION "Patient resource official name must have family name present - BS-PATIENT-RESO-NAME-OFFI"
CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:name[fhir:use/@value="official"]/fhir:given/@value ^.+$
ANNOTATION "Patient resource official name must have at least one given name - BS-PATIENT-RESO-NAME-OFFI"

CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:contact/fhir:telecom/fhir:system/@value="phone")>=1 true
ANNOTATION "Patient resource must have a contact set with a system of phone - BS-PATIENT-RESO-PHONE"
CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:contact/fhir:telecom[fhir:system/@value="phone"]/fhir:value/@value ^[ 0-9]+$
ANNOTATION "Patient resource contact value must be a valid UK phone number - BS-PATIENT-RESO-PHONE"

CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:address
ANNOTATION "Patient resource must have at least one address - BS-PATIENT-RESO-ADDRESS"

CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:address[fhir:use/@value="home"]) 1
ANNOTATION "Patient resource must have at exactly one address with use set to home - BS-PATIENT-RESO-HOME"

CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:address[fhir:use/@value="home"]/fhir:postalCode/@value ^[ 0-9a-zA-Z]+$
ANNOTATION "The home address of the Patient resource must have a valid postcode - BS-PATIENT-RESO-POSTCODE"

CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:birthDate
ANNOTATION "Patient resource must have a date of birth value - BS-PATIENT-RESO-DOB"

#==========================================================================================
# cancel booking request payload must include the required resources

SUBSET booking_cancel_request_payload

CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:MessageHeader) 1
ANNOTATION "Cancel booking request payload must include a MessageHeader resource"
CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient) 1
ANNOTATION "Cancel booking request payload must include a Patient resource"
CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Organization)>=2 true
ANNOTATION "Cancel booking request payload must include an Organization resource"
CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Appointment) 1
ANNOTATION "Cancel request payload must include an Appointment resource"

#==========================================================================================
# the booking request must contain specific Ids if using the stateful sentinel value (9728928254)

SUBSET booking_request_ids
IF xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient/fhir:identifier/fhir:value/@value 9728928254
THEN
	CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:Slot/fhir:id/@value da83ae28-46f0-4aad-9c54-dcad462cafcb
	ANNOTATION "Booking request must contain the correct Slot Id"
	CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:Schedule/fhir:id/@value 7e8c4baa-b7a7-4a7c-bb8c-8c8426ad7781
	ANNOTATION "Booking request must contain the correct Schedule Id"
	CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:HealthcareService/fhir:id/@value 5088769a-491e-463f-a167-fff78bb472d9
	ANNOTATION "Booking request must contain the correct HealthcareService Id"
	CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:Practitioner/fhir:id/@value cad5a61c-3797-4f26-8921-ae4bc7f75eb6
	ANNOTATION "Booking request must contain the correct Practitioner Id"
	CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:PractitionerRole/fhir:id/@value 6bea99e7-b97f-4ee1-980d-05f997afff4f
	ANNOTATION "Booking request must contain the correct PractitionerRole Id"
	CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:Location/fhir:id/@value d23eac9a-12e7-46c5-8781-c3c1d9b1d3c5
	ANNOTATION "Booking request must contain the correct Location Id"
ENDIF

#==========================================================================================
# CHBE53 SUBSET for NBRS2814

SUBSET servicerequest_category_referraltopharmacy


IF xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding/fhir:code[@value='referraltopharmacy' or @value='a5t1' or @value='a5t2' or @value='a5t3']) true
THEN	

	CHECK xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:MessageHeader/fhir:focus/fhir:reference[@value=/fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/../../fhir:fullUrl/@value]) true
	ANNOTATION Focus resource must be the ServiceRequest
	CHECK xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:MessageHeader/fhir:eventCoding/fhir:code/@value='servicerequest-request') true
	ANNOTATION messageHeader.eventCoding.code must be 'servicerequest-request'
	CHECK xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:MessageHeader/fhir:reason/fhir:coding/fhir:code/@value='new') true
	ANNOTATION MessageHeader.reason.coding.code must be 'new'
	CHECK xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Encounter/fhir:status/@value='new' or 'finished') true
	ANNOTATION Encounter.status must be 'triaged' or 'finished'


	IF xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding/fhir:code[@value='referraltopharmacy']) true
	THEN
		CHECK xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding/fhir:code[@value='referraltopharmacy']) true
		ANNOTATION "Service Request Category is referraltopharmacy"
		CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding[fhir:code/@value='referraltopharmacy']/fhir:system/@value https://fhir.nhs.uk/CodeSystem/usecases-categories-bars
		ANNOTATION "referraltopharmacy category codesystem must be populated with https://fhir.nhs.uk/CodeSystem/usecases-categories-bars"
		CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding[fhir:code/@value='referraltopharmacy']/fhir:display/@value Referral to Pharmacy - Deprecated
		ANNOTATION "referraltopharmacy category display must be populated with Referral to Pharmacy - Deprecated"
	
	ENDIF

	IF xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding/fhir:code[@value='a5t1']) true
	THEN
		CHECK xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding/fhir:code[@value='a5t1']) true
		ANNOTATION "Service Request Category is a5t1"
		CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding[fhir:code/@value='a5t1']/fhir:system/@value https://fhir.nhs.uk/CodeSystem/usecases-categories-bars
		ANNOTATION "a5t1 category codesystem must be populated with https://fhir.nhs.uk/CodeSystem/usecases-categories-bars"
		CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding[fhir:code/@value='a5t1']/fhir:display/@value Primary Care to Community Pharmacy
		ANNOTATION "a5t1 category display must be populated with Primary Care to Community Pharmacy"
	
	ENDIF

	IF xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding/fhir:code[@value='a5t2']) true
	THEN
		CHECK xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding/fhir:code[@value='a5t2']) true
		ANNOTATION "Service Request Category is a5t2"
		CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding[fhir:code/@value='a5t2']/fhir:system/@value https://fhir.nhs.uk/CodeSystem/usecases-categories-bars
		ANNOTATION "a5t2 category codesystem must be populated with https://fhir.nhs.uk/CodeSystem/usecases-categories-bars"
		CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding[fhir:code/@value='a5t2']/fhir:display/@value Primary Care to Pharmacy Contraception
		ANNOTATION "a5t2 category display must be populated with Primary Care to Community Pharmacy"
	ENDIF	

	IF xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding/fhir:code[@value='a5t3']) true
	THEN
		CHECK xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding/fhir:code[@value='a5t3']) true
		ANNOTATION "Service Request Category is a5t3"
		CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding[fhir:code/@value='a5t3']/fhir:system/@value https://fhir.nhs.uk/CodeSystem/usecases-categories-bars
		ANNOTATION "a5t3 category codesystem must be populated with https://fhir.nhs.uk/CodeSystem/usecases-categories-bars"
		CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding[fhir:code/@value='a5t3']/fhir:display/@value Primary Care to Pharmacy Blood Pressure Check Service
		ANNOTATION "a5t3 category display must be populated with Primary Care to Community Pharmacy"	
	ENDIF
	
	#referraltopharmacy ServiceRequest.category is present then run GP-Pharmacy Validation checks
	#See https://simplifier.net/guide/nhsbookingandreferralstandard/Home/Applications/BaRS-Applications/Applications/BaRS-APP5?version=1.5.0

	#Gp to Pharmacy Validation checks for payload
	#{ "system": "https://fhir.nhs.uk/CodeSystem/usecases-categories-bars", "code": "referraltopharmacy", "display": "Referral to Pharmacy" }
	#ServiceRequest.category must have two entries, one populated with a value from https://simplifier.net/nhsbookingandreferrals/usecases-categories-bars
	


	#new FHIR resource - Task and Condition - must exist 
	CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Task
	ANNOTATION "referraltopharmacy referral request payload must include a Task resource"
	CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Condition
	ANNOTATION "referraltopharmacy referral request payload must include a Condition resource"

	#Task.Code must be populated with SNOMED coded value`
	CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:Task/fhir:code/fhir:coding/fhir:system/@value http://snomed.info/sct
	ANNOTATION "referraltopharmacy category Task.code.system must be populated with http://snomed.info/sct"
	
	#Condition.Category must be populated with a SNOMED coded value
	CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:Condition/fhir:category/fhir:coding/fhir:system/@value http://snomed.info/sct
	ANNOTATION "referraltopharmacy category Condition.category must be populated with http://snomed.info/sct"

	#Condition.Code must be populated
	CHECK xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:Condition/fhir:code/fhir:text/@value .+
	ANNOTATION "referraltopharmacy category Condition.code.system must be populated"


#	#CarePlan.activity.reference must be populated with a reference to Task
#	CHECK xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:CarePlan/fhir:activity/fhir:reference/fhir:reference[@value=/fhir:Bundle/fhir:entry[fhir:resource/fhir:Task]/fhir:fullUrl/@value]) true
#	ANNOTATION "referraltopharmacy CarePlan.activity.reference must be populated with a reference to Task"
#	#CarePlan.addresses must be populated with a reference to Condition
#	CHECK xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:CarePlan/fhir:addresses/fhir:reference[@value=/fhir:Bundle/fhir:entry[fhir:resource/fhir:Condition]/fhir:fullUrl/@value]) true
#	ANNOTATION "referraltopharmacy CarePlan.addresses must be populated with a reference to Condition"

ENDIF

#==========================================================================================
# CHBE53 SUBSET for CAD to CAD

SUBSET servicerequest_category_CADtoCAD

IF xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding/fhir:code[@value='a6t1']) true
THEN
	CHECK xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest/fhir:category/fhir:coding/fhir:code[@value='a6t1']) true
	ANNOTATION Referral is category a6t1
	
	CHECK xslt TKW_ROOT/config/FHIR_BaRS/validator_config/CarePlan_period_start_dateTime.xslt Failed

	
	IF xpathmatches /fhir:Bundle/fhir:entry/fhir:resource/fhir:QuestionnaireResponse/fhir:questionnaire/@value ^https:\/\/fhir\.nhs\.uk\/Questionnaire\/(PTS|NOC)$
	THEN
	
	IF xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:QuestionnaireResponse[fhir:questionnaire/@value='https://fhir.nhs.uk/Questionnaire/PTS']) true
	THEN
	
	CHECK xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:QuestionnaireResponse[fhir:questionnaire/@value='https://fhir.nhs.uk/Questionnaire/PTS']/fhir:status[@value='completed']) true
	ANNOTATION "If PTS QuestionnaireResponse is present its status must be set to 'completed'"
	
	
	CHECK xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:QuestionnaireResponse[fhir:questionnaire/@value='https://fhir.nhs.uk/Questionnaire/PTS']/fhir:extension/fhir:valueCodeableConcept/fhir:text[contains(@value, 'Pre Triage Sieve') or contains(@value, 'PTS')]) true
	ANNOTATION "If PTS QuestionnaireResponse is present its QuestionnaireResponse.extension.valueCodeableConcept.text must contain 'Pre Triage Sieve' or 'PTS'"
	
	ENDIF

	IF xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:QuestionnaireResponse[fhir:questionnaire/@value='https://fhir.nhs.uk/Questionnaire/NOC']) true
	THEN
		CHECK xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:QuestionnaireResponse[fhir:questionnaire/@value='https://fhir.nhs.uk/Questionnaire/NOC']/fhir:status[@value='completed']) true
		ANNOTATION "If NOC QuestionnaireResponse is present its status must be set to 'completed'"
		CHECK xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:QuestionnaireResponse[fhir:questionnaire/@value='https://fhir.nhs.uk/Questionnaire/NOC']/fhir:extension/fhir:valueCodeableConcept/fhir:text[contains(@value, 'Nature of Call') or contains(@value, 'NOC')]) true
		ANNOTATION "If NOC QuestionnaireResponse is present its QuestionnaireResponse.extension.valueCodeableConcept.text must contain 'Nature of Call' or 'NOC'"
	
	ENDIF
	ELSE
		
		CHECK xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:QuestionnaireResponse/fhir:questionnaire/@value ^https:\/\/fhir\.nhs\.uk\/Questionnaire\/(PTS|NOC)$
		ANNOTATION "QuestionnaireResponse.questionnaire is not PTS OR NOC"
	ENDIF

	CHECK xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:MessageHeader/fhir:eventCoding[fhir:system/@value='https://fhir.nhs.uk/CodeSystem/message-events-bars'][fhir:code/@value='servicerequest-request']) true
	ANNOTATION "MessageHeader.eventCode must be set to servicerequest-request" 

	CHECK xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:MessageHeader[fhir:definition/@value='https://fhir.nhs.uk/MessageDefinition/bars-message-servicerequest-request-referral']) true
	ANNOTATION "MessageHeader.definition must be set to 'https://fhir.nhs.uk/MessageDefinition/bars-message-servicerequest-request-referral'"

	CHECK xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:MessageHeader/fhir:reason/fhir:coding[fhir:system/@value='https://fhir.nhs.uk/CodeSystem/message-reason-bars']/fhir:code[@value='new' or @value='update' or @value='delete']) true
	ANNOTATION "Reason code must be set to new, update or delete"

	CHECK xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Location/fhir:meta[fhir:profile/@value='https://fhir.hl7.org.uk/StructureDefinition/UKCore-Location'] [fhir:profile/@value='https://fhir.hl7.org.uk/StructureDefinition/BARSLocation-incident-location']) true
	ANNOTATION "Incident Location must be set to 'https://fhir.hl7.org.uk/StructureDefinition/UKCore-Location' AND 'https://fhir.hl7.org.uk/StructureDefinition/BARSLocation-incident-location' "

	CHECK xpathequals boolean(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Flag/fhir:meta[fhir:profile/@value='http://hl7.org/fhir/StructureDefinition/Flag'][fhir:profile/@value='https://fhir.nhs.uk/StructureDefinition/BARSFlag-scene-safety']) true
	ANNOTATION "Scene Safety must be set to 'http://hl7.org/fhir/StructureDefinition/Flag' AND 'https://fhir.nhs.uk/StructureDefinition/BARSFlag-scene-safety' "


	CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:ServiceRequest) 1
	ANNOTATION "ServiceRequest resource count must be 1"

	CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Encounter/fhir:identifier)>0 true
	ANNOTATION "Encounter resource count must be at least 1"

	CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Encounter/fhir:identifier)<=2 true
	ANNOTATION "Encounter resource count must be at most 2"

	CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:CarePlan) 1
	ANNOTATION "CarePlan resource count must be 1"

	CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Patient) 1
	ANNOTATION "Patient resource count must be 1"

	CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Condition) 1
	ANNOTATION "Condition resource count must be 1"

	CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Organization)>=2 true
	ANNOTATION "Organization resource count must be at least 2"

	CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Practitioner) 1
	ANNOTATION "Practitioner resource count must be 1"

	CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:PractitionerRole) 1
	ANNOTATION "PractitionerRole resource count must be 1"

	CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Flag/fhir:meta[fhir:profile/@value='http://hl7.org/fhir/StructureDefinition/Flag'][fhir:profile/@value='https://fhir.nhs.uk/StructureDefinition/BARSFlag-scene-safety']) 1
	ANNOTATION "Scene Safety resource count must be 1"

	CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Consent) 1
	ANNOTATION "Consent resource count must be 1"

	CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:HealthcareService) 1
	ANNOTATION "HealthcareService resource count must be 1"

	CHECK xpathequals count(/fhir:Bundle/fhir:entry/fhir:resource/fhir:Location/fhir:meta[fhir:profile/@value='https://fhir.hl7.org.uk/StructureDefinition/UKCore-Location'][fhir:profile/@value='https://fhir.hl7.org.uk/StructureDefinition/BARSLocation-incident-location']) 1
	ANNOTATION "Incident Location resource count must be 1"	


ENDIF

SUBSET messdefcheck
IF xpathequals /fhir:Bundle/fhir:entry/fhir:resource/fhir:MessageHeader/fhir:definition/@value https://fhir.nhs.uk/MessageDefinition/bars-message-servicerequest-request-referral
THEN
	CHECK xslt TKW_ROOT/config/FHIR_BaRS/validator_config/MessageDefinition_referral.xslt NOT
ENDIF


#
#==========================================================================================
# Common validations

INCLUDE TKW_ROOT/config/FHIR_BaRS/validator_config/bars_common.conf
